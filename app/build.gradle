buildscript {
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.1'
        classpath 'com.github.spotbugs.snom:spotbugs-gradle-plugin:5.1.4'
        classpath 'com.puppycrawl.tools:checkstyle:10.12.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'idea'

dependencies {

    implementation 'androidx.core:core:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.appcompat:appcompat-resources:1.6.1"
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.android.material:material:1.9.0'

    compileOnly 'javax.annotation:jsr250-api:1.0'
    api 'com.google.guava:guava:32.0.1-jre'
    api 'javax.inject:javax.inject:1'
    api 'org.apache.commons:commons-csv:1.10.0'

    annotationProcessor 'com.google.dagger:dagger-compiler:2.47'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.47'
    api 'com.google.dagger:dagger-android-support:2.47'
    api 'com.google.dagger:dagger-android:2.47'
    api 'com.google.dagger:dagger:2.47'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.3.1'

    // For some reason without this build fails with duplicate classes exception.
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions.incremental = false

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion

        namespace 'me.tsukanov.counter'
        applicationId 'me.tsukanov.counter'
        versionCode rootProject.ext.appVersion
        versionName "${rootProject.ext.appVersion}"

        testInstrumentationRunner "android.test.InstrumentationTestRunner"

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true
            shrinkResources true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

apply plugin: 'com.github.spotbugs'
spotbugs {
    ignoreFailures = true
}

apply plugin: 'checkstyle'
checkstyle {
    configFile rootProject.file('checkstyle.xml')
    ignoreFailures false
    maxWarnings = 0
    showViolations true
    toolVersion '10.12.4'
}

task checkstyle(type: Checkstyle) {
    source 'src/main/java'
    source 'src/test/java'
    include '**/*.java'
    classpath = files()
}

preBuild.dependsOn('checkstyle')
assemble.dependsOn('lint')
check.dependsOn('checkstyle')
