buildscript {
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath 'com.github.spotbugs.snom:spotbugs-gradle-plugin:5.1.4'
        classpath 'com.puppycrawl.tools:checkstyle:10.12.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'idea'

dependencies {

    implementation 'androidx.core:core:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.appcompat:appcompat-resources:1.7.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.android.material:material:1.12.0'

    compileOnly 'javax.annotation:jsr250-api:1.0'
    api 'com.google.guava:guava:33.3.1-jre'
    api 'javax.inject:javax.inject:1'
    api 'org.apache.commons:commons-csv:1.12.0'

    annotationProcessor 'com.google.dagger:dagger-compiler:2.53'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.53'
    api 'com.google.dagger:dagger-android-support:2.53'
    api 'com.google.dagger:dagger-android:2.53'
    api 'com.google.dagger:dagger:2.53'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.14.2'

    // For some reason without this build fails with duplicate classes exception.
    implementation(platform('org.jetbrains.kotlin:kotlin-bom:2.1.0'))
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions.incremental = false

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion

        namespace 'me.tsukanov.counter'
        applicationId 'me.tsukanov.counter'
        versionCode rootProject.ext.appVersion
        versionName "${rootProject.ext.appVersion}"

        testInstrumentationRunner "android.test.InstrumentationTestRunner"

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true
            shrinkResources true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

apply plugin: 'com.github.spotbugs'
spotbugs {
    ignoreFailures = true
}

apply plugin: 'checkstyle'
checkstyle {
    configFile rootProject.file('checkstyle.xml')
    ignoreFailures false
    maxWarnings = 0
    showViolations true
    toolVersion '10.12.4'
}

tasks.register('checkstyle', Checkstyle) {
    source 'src/main/java'
    source 'src/test/java'
    include '**/*.java'
    classpath = files()
}

preBuild.dependsOn('checkstyle')
assemble.dependsOn('lint')
check.dependsOn('checkstyle')
